cmake_minimum_required(VERSION 3.18)

project(
  libwallycore
  VERSION 1.3.1
  DESCRIPTION "A collection of useful primitives for cryptocurrency wallets"
  LANGUAGES C
)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

option(BUILD_SHARED_LIBS "Build as shared library" OFF)
option(WALLYCORE_ENABLE_TESTS "Build tests" OFF)
option(WALLYCORE_INSTALL "Enable install" OFF)
option(WALLYCORE_COVERAGE "Enable coverage" OFF)
option(WALLYCORE_BUILD_ELEMENTS "Build elements" ON)

if (NOT BUILD_SHARED_LIBS)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
endif()

include(cmake/utils.cmake)
generate_config_file()
configure_file(src/ccan_config.h ccan_config.h COPYONLY)

# libsec256k1-zkp
set(SECP256K1_ENABLE_MODULE_ECDH ON)
set(SECP256K1_ENABLE_MODULE_RECOVERY ON)
set(SECP256K1_ENABLE_MODULE_EXTRAKEYS ON)
set(SECP256K1_ENABLE_MODULE_SCHNORRSIG ON)
set(SECP256K1_ENABLE_MODULE_ELLSWIFT ON)
set(SECP256K1_ENABLE_MODULE_GENERATOR ON)
set(SECP256K1_ENABLE_MODULE_RANGEPROOF ON)
set(SECP256K1_ENABLE_MODULE_SURJECTIONPROOF ON)
set(SECP256K1_ENABLE_MODULE_WHITELIST ON)
set(SECP256K1_ENABLE_MODULE_MUSIG ON)
set(SECP256K1_ENABLE_MODULE_ECDSA_ADAPTOR ON)
set(SECP256K1_ENABLE_MODULE_ECDSA_S2C ON)
set(SECP256K1_ENABLE_MODULE_BPPP ON)
set(SECP256K1_BUILD_BENCHMARK OFF)
set(SECP256K1_BUILD_TESTS OFF)
set(SECP256K1_BUILD_EXHAUSTIVE_TESTS OFF)
set(SECP256K1_BUILD_CTIME_TESTS OFF)
set(SECP256K1_BUILD_EXAMPLES OFF)
set(SECP256K1_INSTALL ${WALLYCORE_INSTALL})
add_subdirectory(./src/secp256k1/)

add_subdirectory(./src)

if(NOT WALLYCORE_ENABLE_TESTS)
    return()
endif()

enable_testing()
add_subdirectory(src/ctest)
