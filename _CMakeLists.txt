cmake_minimum_required(VERSION 3.20)

project(
  libwallycore
  VERSION 0.9.2
  DESCRIPTION "A collection of useful primitives for cryptocurrency wallets"
  LANGUAGES C
)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

option(BUILD_SHARED_LIBS "Build as shared library" OFF)

include(cmake/utils.cmake)
generate_config_file()
configure_file(src/ccan_config.h ccan_config.h COPYONLY)

include(cmake/libsecp256k1.cmake)

add_subdirectory(src)

if (CALL_FROM_SETUP_PY)
    target_compile_definitions(wallycore PRIVATE SWIG_PYTHON_BUILD)
    add_subdirectory(bindings/python)
    return()
endif()


### install directives
include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

set(LIB_CMAKE_INSTALL_DIR ${CMAKE_INSTALL_LIBDIR}/cmake/wallycore)

configure_package_config_file(
    ${CMAKE_SOURCE_DIR}/cmake/wallycore-config.cmake.in
    "${CMAKE_CURRENT_BINARY_DIR}/wallycore-config.cmake"
    INSTALL_DESTINATION ${LIB_CMAKE_INSTALL_DIR}
    PATH_VARS LIB_CMAKE_INSTALL_DIR
)
write_basic_package_version_file(
    wallycore-config-version.cmake
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY SameMajorVersion
)

install(
    FILES
        "${CMAKE_CURRENT_BINARY_DIR}/wallycore-config.cmake"
        "${CMAKE_CURRENT_BINARY_DIR}/wallycore-config-version.cmake"
    DESTINATION
        ${LIB_CMAKE_INSTALL_DIR}
)
install(
    TARGETS wallycore
    EXPORT "wallycore-target"
    COMPONENT wallycore
    RUNTIME EXCLUDE_FROM_ALL
    OBJECTS EXCLUDE_FROM_ALL
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
install(EXPORT "wallycore-target"
    DESTINATION ${LIB_CMAKE_INSTALL_DIR}
    NAMESPACE wallycore::
    FILE "wallycore-targets.cmake"
)

install(
    FILES $<TARGET_PROPERTY:libsecp256k1,IMPORTED_LOCATION>
    DESTINATION ${CMAKE_INSTALL_LIBDIR}
    COMPONENT libsecp256k1
)
install(
    FILES $<TARGET_PROPERTY:libsecp256k1,PUBLIC_HEADER>
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT libsecp256k1
)
install(
    FILES ${INSTALL_DIR}/lib/pkgconfig/libsecp256k1.pc
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig
    COMPONENT libsecp256k1
)
install(SCRIPT ${CMAKE_SOURCE_DIR}/_cmake/libsecp256k1-pkgconfig.cmake
    COMPONENT libsecp256k1
)
